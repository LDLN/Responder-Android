apply plugin: 'com.android.application'
apply plugin: 'realm-android'

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.0"

    defaultConfig {
        applicationId "co.ldln.android"
        minSdkVersion 16 // Required by rncryptor lib
        targetSdkVersion 23
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    packagingOptions {
        // avoid duplicate conflict with rncryptor-native
        // see: https://github.com/ericwlange/AndroidJSCore/issues/22#issuecomment-217689988
        // also: http://stackoverflow.com/a/34525498/604003
        // also: http://stackoverflow.com/a/37107495/604003
        pickFirst 'lib/armeabi-v7a/libgnustl_shared.so'
    }
}

dependencies {
    compile 'com.github.tgio:rncryptor-native:0.0.8'
    compile 'com.android.support:support-v4:24.1.0'
    compile 'com.squareup.okhttp:okhttp:2.3.0'
    compile(name:'WhirlyGlobeMaply', ext:'aar')
}

// lines below this comment are to
// avoid duplicate conflict with rncryptor-native
// see: http://stackoverflow.com/a/39584838/604003
import com.android.build.gradle.internal.pipeline.TransformTask

def deleteDuplicateJniFiles() {
    def files = fileTree("${buildDir}/intermediates/exploded-aar/com.github.tgio/rncryptor-native/0.0.8/jni/") {
        include "**/libgnustl_shared.so"
    }
    files.each { it.delete() }
}

tasks.withType(TransformTask) { pkgTask ->
    pkgTask.doFirst { deleteDuplicateJniFiles() }
}